plugins {
    id 'com.github.psxpaul.execfork' version '0.1.12'
}
apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "net.cacheux.screenshotserver"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task createJar(type: Copy, dependsOn: 'assemble') {
    from "${project.buildDir}/outputs/apk/debug/"
    into "${project.buildDir}/outputs/libs/"
    include 'screenshotServer-debug.apk'
    rename 'screenshotServer-debug.apk', 'screenshotServer.jar'
}

task installServer(type: Exec, dependsOn: 'createJar') {
    commandLine 'adb', 'push', "${project.buildDir}/outputs/libs/screenshotServer.jar", "/data/local/tmp"
}

task installKillScript(type: Exec) {
    commandLine 'adb', 'push', "${project.rootDir}/scripts/killServer.sh", "/data/local/tmp"
}

task killServer(type: Exec, dependsOn: 'installKillScript') {
    commandLine 'adb', 'shell', 'sh', '/data/local/tmp/killServer.sh'
}

task runServer(type: com.github.psxpaul.task.ExecFork, dependsOn: ['killServer', 'installServer']) {
    executable = "${project.rootDir}/scripts/runServer.sh"
}

task runServerOtherPort(type: com.github.psxpaul.task.ExecFork, dependsOn: ['killServer', 'installServer']) {
    executable = "${project.rootDir}/scripts/runServer.sh"
    args = [ "51000" ]
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

afterEvaluate {
    tasks.each { task ->
        if (task.name == 'connectedDebugAndroidTest') {
            task.dependsOn(runServerOtherPort)
        }
    }
}